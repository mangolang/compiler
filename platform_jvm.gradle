
/**
 * This file contains shared configuration for JVM platform modules.
 */

project.ext.jvm_submods = subprojects.findAll {
    it.name.endsWith('-jvm')
}
println("JVM submodules: ${fmtMods(jvm_submods)}")


/* All JVM platform subprojects. */
configure(jvm_submods) {
    apply plugin: 'kotlin-platform-jvm'

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        testCompile 'junit:junit:4.12'
        testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
        testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    }

    def (parentCommon, depNames) = rootProject.deriveDependencyNames(project)

    dependencies {
        expectedBy parentCommon
        compile depNames.collect { project(it) }
    }

    test.dependsOn depNames.collect { "${it}:test" }
}

